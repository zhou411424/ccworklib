apply plugin: 'com.android.library'

//上传到jcenter需要的配置
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//项目主页
def siteUrl = 'https://github.com/zhou411424/ccworklib'
//项目的版本控制地址
def gitUrl = 'https://github.com/zhou411424/ccworklib.git'

//发布到组织名称，必须填写
group = "com.ccworklib"
//发布到JCenter上的项目名字，必须填写
def libName = "ccworklib"
//根节点添加版本号，下次更新时只需要更改版本号即可
version = "1.0.0"
/**上面配置后上传到jcenter后的编译路径是这样的： compile 'com.ccworklib:ccworklib:1.0.0' */

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.+'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
}

/** 以下也是将Android Library上传到jcenter的相关配置*/
//生成源文件（打包sourcesJar）
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"//避免提示：编码GBK的不可映射字符->请正确配置javadoc编码
    options.charSet 'UTF-8'
    options.author true
    options.version true
    failOnError false//忽略错误配置
}
// 文档打包成jar (打包javadocJar)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}
//上传到jcenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

//配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'android lib'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url siteUrl
                    }
                }
                developers {
                    developer {
                        id 'zhou411424'
                        name 'zhou411424'
                        email 'zhoulc668@163.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到jcenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")//读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")//读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = libName //发布到JCenter上的项目名字，必须填写
        desc = 'This is a generic tool library'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
